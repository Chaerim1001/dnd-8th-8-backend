plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.1'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'checkstyle'
    id 'jacoco'
    id 'org.sonarqube' version '3.5.0.2730'
    id "org.asciidoctor.jvm.convert" version "3.3.2"
}

group = 'com.dnd'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web:3.0.1'

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'

    // database
    runtimeOnly 'com.mysql:mysql-connector-j:8.0.31'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc:3.0.1'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:3.0.1'
    runtimeOnly 'com.h2database:h2:2.1.214'

    implementation 'org.springframework.boot:spring-boot-starter-data-redis:3.0.0'

    // flyway
    implementation 'org.flywaydb:flyway-mysql:9.11.0'

    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.0.1'

    // rest docs
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    // spring security
    implementation 'org.springframework.boot:spring-boot-starter-security:3.0.1'
    testImplementation 'org.springframework.security:spring-security-test:6.0.1'

    // OAuth2
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client:3.0.1'

    // jwt
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
}

checkstyle {
    maxWarnings = 0
    toolVersion = "10.4"
}

tasks.named('test') {
    useJUnitPlatform()
    outputs.dir snippetsDir
}

ext {
    snippetsDir = file('build/generated-snippets')
}

asciidoctor {
    configurations 'asciidoctorExt'
    baseDirFollowsSourceFile()
    inputs.dir snippetsDir
    dependsOn test
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

sonar {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'dnd-side-project'
        property 'sonar.projectKey', 'dnd-side-project_dnd-8th-8-backend'
        property 'sonar.java.checkstyle.reportPaths', 'build/reports/checkstyle/main.xml'
        property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/test/jacocoTestReport.xml'
    }
}
